buildscript {
    repositories {
        mavenCentral()
        maven {
            url = "https://repo.spongepowered.org/maven/"
        }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '5.1.+'
}

apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base {
    archivesName = mod_id + "-" + mod_version + "-" + minecraft_version_group + "-FORGE"
}

minecraft {
    mappings channel: "official", version: minecraft_version

    //copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}")
    compileOnly fg.deobf("curse.maven:server-browser-825617:${server_browser_artifact_id_forge}")

    implementation project(':Common')
}

processResources {
    from project(":Common").sourceSets.main.resources

    inputs.property "version", mod_version
    inputs.property "loader_version_range", forge_loader_version_range
    inputs.property "forge_version_range", forge_version_range
    inputs.property "minecraft_version_range", forge_minecraft_version_range

    filesMatching("META-INF/mods.toml") {
        expand  version: mod_version,
                loader_version_range: forge_loader_version_range,
                forge_version_range: forge_version_range,
                minecraft_version_range: forge_minecraft_version_range
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "servercountryflags",
                "Specification-Vendor"    : "Khajiitos",
                "Implementation-Title"    : "Server Country Flags",
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : "Khajiitos",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)

    it.options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    source(project(":Common").sourceSets.main.allJava)
}

// Ideally this project would use Gradle from the root project
tasks.register('wrapper', Wrapper) {
    gradleVersion = '7.6.2'
}

// Register dummy ideaSyncTask so that Gradle doesn't complain
// but why does it complain here and not in the Fabric project?
tasks.register('ideaSyncTask', {})

jar.finalizedBy('reobfJar')