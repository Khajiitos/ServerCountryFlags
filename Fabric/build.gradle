plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

base {
    archivesName = mod_id + "-" + mod_version + "-" + minecraft_version_group + "-FABRIC";
}

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation project(":Common")
}

processResources {
    from project(":Common").sourceSets.main.resources

    filteringCharset "UTF-8"

    inputs.property "version", mod_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "minecraft_version_range", fabric_minecraft_version_range

    filesMatching("fabric.mod.json") {
        expand  "version": mod_version,
                "minecraft_version": minecraft_version,
                "minecraft_version_range": fabric_minecraft_version_range
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)

    it.options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    source(project(":Common").sourceSets.main.allJava)
}

// Ideally this project would use Gradle from the root project
tasks.register('wrapper', Wrapper) {
    gradleVersion = '7.6.2'
}

jar.finalizedBy('remapJar')